from os.path import join as pjoin
import sys

DATA_DIR = "../data/"
RAW_DATA_DIR = pjoin(DATA_DIR, "raw")
RAW_LARGE_DATA_DIR = pjoin(RAW_DATA_DIR, 'large')
INTERIM_DATA_DIR = pjoin(DATA_DIR, "interim")
INTERIM_LARGE_DATA_DIR = pjoin(INTERIM_DATA_DIR, 'large')
PROCESSED_DATA_DIR = pjoin(DATA_DIR, "processed")

###############################################################################
# Raw Data
###############################################################################

# SSCI journals 
SSCI = pjoin(RAW_DATA_DIR, 'ssci.csv')

ROR_RAW_DATA = pjoin(RAW_LARGE_DATA_DIR, 'ror.json')

CAT_CLASSIFICATION = pjoin(RAW_DATA_DIR, 'category-classification.csv')
TO_DELETE_DOIS = pjoin(RAW_DATA_DIR, 'to_delete_dois.txt')

###############################################################################
# Interim outputs
###############################################################################

ICA_PAPER_DF = pjoin(INTERIM_DATA_DIR, 'ica_paper_df.csv')

ICA_PAPER_DATA = pjoin(INTERIM_DATA_DIR, 'ica_paper_data.csv')
ICA_AUTHOR_DATA = pjoin(INTERIM_DATA_DIR, 'ica_author_data.csv')
ICA_ERROR_URLS = pjoin(INTERIM_DATA_DIR, 'ica_error_urls.txt')

ICA_AUTHOR_DF = pjoin(INTERIM_DATA_DIR, 'ica_author_df.csv')
NO_AUTHOR_URLS = pjoin(INTERIM_DATA_DIR, 'no_author_urls.txt')

AUTHOR_WITH_GENDER = pjoin(INTERIM_DATA_DIR, 'author_with_gender.csv')

AUTHOR_WITH_PRED_RAW = pjoin(INTERIM_DATA_DIR, 'author_with_pred_raw.csv')

###############################################################################
# Processed outputs
###############################################################################

AUTHOR_WITH_PRED = pjoin(PROCESSED_DATA_DIR, 'author_with_pred.csv')
ICA_PAPER_DF_WITH_TYPE = pjoin(PROCESSED_DATA_DIR, 'ica_paper_df_with_type.csv')
RESEARCH_AUTHOR_WITH_PRED = pjoin(PROCESSED_DATA_DIR, 'research_author_with_pred.csv')

###############################################################################
# Workflows
###############################################################################

rule all:
    input:
        ICA_PAPER_DF,
        # ICA_AUTHOR_DF,
        # NO_AUTHOR_URLS, 
        ICA_PAPER_DATA,
        ICA_AUTHOR_DATA,
        ICA_ERROR_URLS,
        AUTHOR_WITH_GENDER,
        AUTHOR_WITH_PRED_RAW,
        AUTHOR_WITH_PRED,
        ICA_PAPER_DF_WITH_TYPE,
        RESEARCH_AUTHOR_WITH_PRED,

rule scrape_ica_paper_dois:
    output: ICA_PAPER_DF
    shell: "python scripts/scrape_ica_paper_dois.py {output}"

rule get_ica_author_df:
    input: ICA_PAPER_DF
    output: ICA_AUTHOR_DF, NO_AUTHOR_URLS
    shell: "python scripts/get_ica_author_df.py {input} {output}"

rule scrape_ica_author_data:
    input: ICA_PAPER_DF
    output: ICA_PAPER_DATA, ICA_AUTHOR_DATA, ICA_ERROR_URLS
    shell: "python scripts/scrape_ica_author_data.py {input} {output}"

rule get_author_with_gender:
    input: ICA_AUTHOR_DATA, 
    output: AUTHOR_WITH_GENDER
    shell: "python scripts/get_author_with_gender.py {input} {output}"

rule get_author_with_pred_raw:
    input: 
        AUTHOR_WITH_GENDER, 
        ROR_RAW_DATA
    output: AUTHOR_WITH_PRED_RAW
    shell: "python scripts/get_author_with_pred_raw.py {input} {output}"

rule get_author_with_pred:
    input: AUTHOR_WITH_PRED_RAW
    output: AUTHOR_WITH_PRED
    shell: "python scripts/get_author_with_pred.py {input} {output}"

rule get_research_paper_only:
    input: 
        CAT_CLASSIFICATION,
        TO_DELETE_DOIS,
        ICA_PAPER_DF,
        AUTHOR_WITH_PRED
    output: ICA_PAPER_DF_WITH_TYPE, RESEARCH_AUTHOR_WITH_PRED
    shell: "python scripts/get_research_paper_only.py {input} {output}"
