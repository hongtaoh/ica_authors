from os.path import join as pjoin
import sys

DATA_DIR = "../data/"
RAW_DATA_DIR = pjoin(DATA_DIR, "raw")
RAW_LARGE_DATA_DIR = pjoin(RAW_DATA_DIR, 'large')
INTERIM_DATA_DIR = pjoin(DATA_DIR, "interim")
INTERIM_LARGE_DATA_DIR = pjoin(INTERIM_DATA_DIR, 'large')
PROCESSED_DATA_DIR = pjoin(DATA_DIR, "processed")

###############################################################################
# Raw Data
###############################################################################

# SSCI journals 
SSCI = pjoin(RAW_DATA_DIR, 'ssci.csv')

ROR_RAW_DATA = pjoin(RAW_LARGE_DATA_DIR, 'ror.json')

PAPER_CLASSIFICATION_HONGTAO = pjoin(RAW_DATA_DIR, 'paper_classification_hongtao.csv')
PAPER_CLASSIFICATION_JEFF = pjoin(RAW_DATA_DIR, 'paper_classification_jeff.csv')
PAPER_CLASSIFICATION_KRISTEN = pjoin(RAW_DATA_DIR, 'paper_classification_kristen.csv')

###############################################################################
# Interim outputs
###############################################################################

ICA_PAPER_DF = pjoin(INTERIM_DATA_DIR, 'ica_paper_df.csv')

ICA_PAPER_DATA = pjoin(INTERIM_DATA_DIR, 'ica_paper_data.csv')
ICA_AUTHOR_DATA = pjoin(INTERIM_DATA_DIR, 'ica_author_data.csv')
ICA_ERROR_URLS = pjoin(INTERIM_DATA_DIR, 'ica_error_urls.txt')

ICA_AUTHOR_DF = pjoin(INTERIM_DATA_DIR, 'ica_author_df.csv')
NO_AUTHOR_URLS = pjoin(INTERIM_DATA_DIR, 'no_author_urls.txt')

AUTHOR_WITH_GENDER = pjoin(INTERIM_DATA_DIR, 'author_with_gender.csv')

AUTHOR_WITH_PRED_RAW = pjoin(INTERIM_DATA_DIR, 'author_with_pred_raw.csv')

###############################################################################
# Processed outputs
###############################################################################

AUTHOR_WITH_PRED = pjoin(PROCESSED_DATA_DIR, 'author_with_pred.csv')
ICA_PAPER_DF_WITH_TYPE = pjoin(PROCESSED_DATA_DIR, 'ica_paper_df_with_type.csv')
RESEARCH_PAPER_DF = pjoin(PROCESSED_DATA_DIR, 'research_paper_df.csv')

AUTHOR_WITH_PRED_WITH_TYPE = pjoin(PROCESSED_DATA_DIR, 'author_with_pred_with_type.csv')
RESEARCH_AUTHOR_WITH_PRED = pjoin(PROCESSED_DATA_DIR, 'research_author_with_pred.csv')

###############################################################################
# Workflows
###############################################################################

rule all:
    input:
        ICA_PAPER_DF,
        ICA_PAPER_DATA,
        ICA_AUTHOR_DATA,
        ICA_ERROR_URLS,
        AUTHOR_WITH_GENDER,
        AUTHOR_WITH_PRED_RAW,
        AUTHOR_WITH_PRED,       
        ICA_PAPER_DF_WITH_TYPE,
        AUTHOR_WITH_PRED_WITH_TYPE,

# scrape ica paper dois
rule scrape_ica_paper_dois:
    output: ICA_PAPER_DF
    shell: "python scripts/scrape_ica_paper_dois.py {output}"

# scrape ica author and paper data
rule scrape_ica_author_data:
    input: ICA_PAPER_DF
    output: ICA_PAPER_DATA, ICA_AUTHOR_DATA, ICA_ERROR_URLS
    shell: "python scripts/scrape_ica_author_data.py {input} {output}"

# get author gender raw predictions
rule get_author_with_gender:
    input: ICA_AUTHOR_DATA, 
    output: AUTHOR_WITH_GENDER
    shell: "python scripts/get_author_with_gender.py {input} {output}"

# get race and affiliations. Raw because I'll change column order later
rule get_author_with_pred_raw:
    input: 
        AUTHOR_WITH_GENDER, 
        ROR_RAW_DATA
    output: AUTHOR_WITH_PRED_RAW
    shell: "python scripts/get_author_with_pred_raw.py {input} {output}"

# rearrange colomn order
rule get_author_with_pred:
    input: AUTHOR_WITH_PRED_RAW
    output: AUTHOR_WITH_PRED
    shell: "python scripts/get_author_with_pred.py {input} {output}"

# add 'type' to paper df and author data
rule get_paper_and_author_with_type:
    input: 
        PAPER_CLASSIFICATION_HONGTAO,
        PAPER_CLASSIFICATION_JEFF,
        PAPER_CLASSIFICATION_KRISTEN,
        ICA_PAPER_DF,
        AUTHOR_WITH_PRED,

    output: ICA_PAPER_DF_WITH_TYPE, 
            AUTHOR_WITH_PRED_WITH_TYPE,
            RESEARCH_PAPER_DF,
            RESEARCH_AUTHOR_WITH_PRED,
    shell: "python scripts/get_paper_and_author_with_type.py {input} {output}"